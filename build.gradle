import java.nio.file.Files
import static java.nio.file.StandardCopyOption.REPLACE_EXISTING
/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */
 buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
  id 'java'
  id 'maven-publish'
  id 'distribution'
  id 'idea'
}

group "org.logstash.integrations"

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
}

// given https://docs.confluent.io/current/installation/versions-interoperability.html matrix
// Confluent Platform 7.9.x is Apache Kafka 3.9.x
String confluentKafkaVersion = '7.9.1'
String apacheKafkaVersion = '3.9.1'

configurations.configureEach {
    resolutionStrategy {
        capabilitiesResolution {
            withCapability("org.lz4:lz4-java") {
                select(candidates.find { ((ModuleComponentIdentifier) it.id).group == "at.yawk.lz4" })
            }
        }
    }
}

repositories {
    mavenCentral()
    maven {
        // Confluent repo for kafka-avro-serializer
        url "https://packages.confluent.io/maven/"
    }
}

dependencies {
    implementation("io.confluent:kafka-avro-serializer:${confluentKafkaVersion}") {
        exclude group: 'org.apache.kafka', module:'kafka-clients'
    }
    // dependency of kafka-avro-serializer
    implementation("io.confluent:kafka-schema-serializer:${confluentKafkaVersion}") {
        exclude group: 'org.apache.kafka', module:'kafka-clients'
    }
    // dependency of kafka-avro-serializer
    implementation 'org.apache.avro:avro:1.11.4'
    // dependency of kafka-avro-serializer
    implementation("io.confluent:kafka-schema-registry-client:${confluentKafkaVersion}") {
        exclude group: 'org.apache.kafka', module:'kafka-clients'
    }
    implementation "org.apache.kafka:kafka-clients:${apacheKafkaVersion}"
    // slf4j, zstd, lz4-java, snappy are dependencies from "kafka-clients"
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'com.github.luben:zstd-jni:1.5.6-8'
    implementation 'at.yawk.lz4:lz4-java:1.10.1'
    implementation 'org.xerial.snappy:snappy-java:1.1.10.7'
    // Override transitive dependency version from kafka-avro-serializer chain
    implementation 'org.apache.commons:commons-lang3:3.20.0'
}
task generateGemJarRequiresFile {
    doLast {
        File jars_file = file('lib/logstash-integration-kafka_jars.rb')
        jars_file.newWriter().withWriter { w ->
            w << "# AUTOGENERATED BY THE GRADLE SCRIPT. DO NOT EDIT.\n\n"
            w << "require \'jar_dependencies\'\n"
            configurations.runtimeClasspath.allDependencies.each {
                w << "require_jar(\'${it.group}\', \'${it.name}\', \'${it.version}\')\n"
            }
        }
    }
}

task vendor {
    doLast {
        String vendorPathPrefix = "vendor/jar-dependencies"
        configurations.runtimeClasspath.allDependencies.each { dep ->
            File f = configurations.runtimeClasspath.filter { it.absolutePath.contains("${dep.group}/${dep.name}/${dep.version}") }.singleFile
            String groupPath = dep.group.replaceAll('\\.', '/')
            File newJarFile = file("${vendorPathPrefix}/${groupPath}/${dep.name}/${dep.version}/${dep.name}-${dep.version}.jar")
            newJarFile.mkdirs()
            Files.copy(f.toPath(), newJarFile.toPath(), REPLACE_EXISTING)
        }
    }
}

vendor.dependsOn(generateGemJarRequiresFile)
