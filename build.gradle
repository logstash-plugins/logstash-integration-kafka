import java.nio.file.Files
import static java.nio.file.StandardCopyOption.REPLACE_EXISTING
/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */
apply plugin: "java"
apply plugin: 'maven'
apply plugin: "distribution"
apply plugin: "idea"

group "org.logstash.integrations"

sourceCompatibility = JavaVersion.VERSION_1_8

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
}

repositories {
    mavenCentral()
    maven {
        // Confluent repo for kafka-avro-serializer
        url "https://packages.confluent.io/maven/"
    }
}

dependencies {
    compile('io.confluent:kafka-avro-serializer:5.5.1') {
        exclude group: 'org.apache.kafka', module:'kafka-clients'
    }
    compile('io.confluent:kafka-schema-serializer:5.5.1') {
        exclude group: 'org.apache.kafka', module:'kafka-clients'
    }
    compile 'io.confluent:common-config:5.5.1'
    compile 'org.apache.avro:avro:1.9.2'
    compile('io.confluent:kafka-schema-registry-client:5.5.1') {
        exclude group: 'org.apache.kafka', module:'kafka-clients'
    }
    compile('org.apache.kafka:kafka_2.12:2.5.1') {
        // contains kafka.utils.VerifiableProperties used by kafka-schema-registry-client, explicitly exclude
        // transitive dependencies
        transitive = false
    }
    compile 'io.confluent:common-utils:5.5.1'
    compile 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    compile 'org.glassfish.jersey.core:jersey-common:2.33'
    // given https://docs.confluent.io/current/installation/versions-interoperability.html matrix
    // Confluent Platform 5.5.x is Apache Kafka 2.5.x
    compile 'org.apache.kafka:kafka-clients:2.5.1'
    compile 'com.github.luben:zstd-jni:1.4.4-7'
    compile 'org.slf4j:slf4j-api:1.7.30'
    compile 'org.lz4:lz4-java:1.7.1'
    compile 'org.xerial.snappy:snappy-java:1.1.7.3'
}
task generateGemJarRequiresFile {
    doLast {
        File jars_file = file('lib/logstash-integration-kafka_jars.rb')
        jars_file.newWriter().withWriter { w ->
            w << "# AUTOGENERATED BY THE GRADLE SCRIPT. DO NOT EDIT.\n\n"
            w << "require \'jar_dependencies\'\n"
            configurations.runtime.allDependencies.each {
                w << "require_jar(\'${it.group}\', \'${it.name}\', \'${it.version}\')\n"
            }
        }
    }
}

task vendor {
    doLast {
        String vendorPathPrefix = "vendor/jar-dependencies"
        configurations.runtime.allDependencies.each { dep ->
            File f = configurations.runtime.filter { it.absolutePath.contains("${dep.group}/${dep.name}/${dep.version}") }.singleFile
            String groupPath = dep.group.replaceAll('\\.', '/')
            File newJarFile = file("${vendorPathPrefix}/${groupPath}/${dep.name}/${dep.version}/${dep.name}-${dep.version}.jar")
            newJarFile.mkdirs()
            Files.copy(f.toPath(), newJarFile.toPath(), REPLACE_EXISTING)
        }
    }
}

vendor.dependsOn(generateGemJarRequiresFile)
